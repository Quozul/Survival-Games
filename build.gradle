plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.8.20'
}

group = 'dev.quozul'
version = '0.3-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url = "https://repo.aikar.co/content/groups/aikar/"
    }
}

jar {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    destinationDirectory = file("server/plugins")
    manifest {
        attributes["Main-Class"] = "dev.quozul.UHC.Main"
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.19.4-R0.1-SNAPSHOT"
    compileOnly "co.aikar:acf-paper:0.5.1-SNAPSHOT"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.8.20"
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '17'
    }
}

compileJava {
    options.compilerArgs += ["-parameters"]
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
